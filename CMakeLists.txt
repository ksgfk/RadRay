cmake_minimum_required(VERSION 3.23)

if (POLICY CMP0077)
    cmake_policy(SET CMP0077 NEW)
endif()

project(RadRay)

include(cmake/Utility.cmake)

list(PREPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
if (MSVC)
    add_compile_options(/MP)
endif()

set(RADRAY_BUILD_PATH "" CACHE PATH "RadRay build binary path")
if (NOT RADRAY_BUILD_PATH)
    if (DEFINED CMAKE_VS_MSBUILD_COMMAND)
        set(RADRAY_BUILD_PATH "${CMAKE_BINARY_DIR}/_build" CACHE PATH "RadRay build binary path" FORCE)
    else()
        set(RADRAY_BUILD_PATH "${CMAKE_BINARY_DIR}/_build/${CMAKE_BUILD_TYPE}" CACHE PATH "RadRay build binary path" FORCE)
    endif()
endif()

include(CMakeDependentOption)
option(RADRAY_BUILD_TESTS "Build the RadRay tests" ON)
cmake_dependent_option(RADRAY_BUILD_BENCHMARKS "Build the RadRay benchmarks when release" ON "NOT CMAKE_BUILD_TYPE STREQUAL \"Release\"" OFF)
option(RADRAY_BUILD_WINDOW "Build the RadRay window" ON)
option(RADRAY_BUILD_RENDER "Build the RadRay render" ON)
cmake_dependent_option(RADRAY_ENABLE_D3D12 "Enable Direct3D 12 backend" ON "RADRAY_BUILD_RENDER AND WIN32" OFF)
cmake_dependent_option(RADRAY_ENABLE_VULKAN "Enable Vulkan backend" ON "RADRAY_BUILD_RENDER" OFF)
cmake_dependent_option(RADRAY_ENABLE_DXC "Enable DirectX Shader Compiler support" ON "RADRAY_BUILD_RENDER" OFF)
cmake_dependent_option(RADRAY_ENABLE_SPVCROSS "Enable SPIR-V Cross support" ON "RADRAY_BUILD_RENDER" OFF)
cmake_dependent_option(RADRAY_ENABLE_IMGUI "Enable Dear ImGui support" ON "RADRAY_BUILD_RENDER" OFF)
cmake_dependent_option(RADRAY_DISABLE_IMGUI_DEMO "Enable Dear ImGui demo window" OFF "RADRAY_ENABLE_IMGUI" OFF)
option(RADRAY_ENABLE_MIMALLOC "Enable mimalloc as allocator" ON)
option(RADRAY_ENABLE_ZLIB "Enable zlib support" ON)
cmake_dependent_option(RADRAY_ENABLE_LIBPNG "Enable libpng support" ON "RADRAY_ENABLE_ZLIB" OFF)
cmake_dependent_option(RADRAY_ENABLE_FREETYPE "Enable FreeType support" ON "RADRAY_ENABLE_ZLIB AND RADRAY_ENABLE_LIBPNG" OFF)

# ======== Load manifest ========
if (WIN32)
    set(RADRAY_SDKVAR_PLATFORM "windows")
elseif (APPLE)
    set(RADRAY_SDKVAR_PLATFORM "macos")
elseif (UNIX)
    set(RADRAY_SDKVAR_PLATFORM "linux")
else()
    set(RADRAY_SDKVAR_PLATFORM "unknown")
endif()
if (NOT EXISTS "${CMAKE_SOURCE_DIR}/project_manifest.json")
    message(FATAL_ERROR "manifest file not found: ${CMAKE_SOURCE_DIR}/project_manifest.json")
endif()
file(READ "${CMAKE_SOURCE_DIR}/project_manifest.json" RADRAY_MANIFEST_CONTENT)
string(JSON RADRAY_MANIFEST_ARTIFACTS GET "${RADRAY_MANIFEST_CONTENT}" Artifacts)
string(JSON RADRAY_MANIFEST_ARTIFACTS_LENGTH LENGTH "${RADRAY_MANIFEST_CONTENT}" Artifacts)
math(EXPR RADRAY_MANIFEST_ARTIFACTS_LAST "${RADRAY_MANIFEST_ARTIFACTS_LENGTH} - 1")
foreach(_INDEX RANGE ${RADRAY_MANIFEST_ARTIFACTS_LAST})
    string(JSON _ARTIFACT GET "${RADRAY_MANIFEST_CONTENT}" Artifacts ${_INDEX})
    string(JSON _NAME GET "${_ARTIFACT}" Name)
    string(JSON _PLATFORM GET "${_ARTIFACT}" Platform)
    string(FIND "${_PLATFORM}" "${RADRAY_SDKVAR_PLATFORM}" _PLATFORM_MATCH)
    if (_PLATFORM_MATCH EQUAL -1)
        message(STATUS "Skipping SDK artifact ${_NAME} not for this platform: ${_ARTIFACT}")
        continue()
    endif()
    set(RADRAY_SDK_${_NAME} "${_ARTIFACT}")
    string(JSON _VERSION GET "${_ARTIFACT}" Version)
    set(RADRAY_SDK_${_NAME}_VERSION "${_VERSION}")
    message(STATUS "Loaded SDK artifact ${_NAME} version ${RADRAY_SDK_${_NAME}_VERSION}")
endforeach()
# ===============================

set(RADRAY_THIRDPARTY_ROOT "${CMAKE_SOURCE_DIR}/third_party")

set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)
set(SKIP_INSTALL_ALL ON CACHE BOOL "" FORCE)
include(FetchContent)

set(FETCHCONTENT_UPDATES_DISCONNECTED OFF CACHE BOOL "" FORCE)
set(FMT_MASTER_PROJECT OFF CACHE BOOL "" FORCE)
add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/fmt)
radray_optimize_flags_library(fmt)
radray_set_build_path(fmt)

set(SPDLOG_MASTER_PROJECT OFF CACHE BOOL "" FORCE)
set(SPDLOG_FMT_EXTERNAL ON CACHE BOOL "" FORCE)
set(SPDLOG_NO_EXCEPTIONS ON CACHE BOOL "" FORCE)
set(SPDLOG_NO_THREAD_ID ON CACHE BOOL "" FORCE)
set(SPDLOG_DISABLE_DEFAULT_LOGGER ON CACHE BOOL "" FORCE)
add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/spdlog)
radray_optimize_flags_library(spdlog)
radray_set_build_path(spdlog)

set(PROJECT_IS_TOP_LEVEL OFF CACHE BOOL "" FORCE)
set(EIGEN_BUILD_DOCS OFF CACHE BOOL "" FORCE)
set(EIGEN_BUILD_DOC OFF CACHE BOOL "" FORCE)
add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/eigen)

set(XXHASH_BUILD_XXHSUM OFF CACHE BOOL "" FORCE)
set(DISPATCH ON CACHE BOOL "" FORCE)
add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/xxHash/build/cmake)
radray_optimize_flags_library(xxhash)
radray_set_build_path(xxhash)

add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/sigslot)

set(STDEXEC_BUILD_TESTS OFF CACHE BOOL "" FORCE)
set(STDEXEC_BUILD_DOCS OFF CACHE BOOL "" FORCE)
set(STDEXEC_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(STDEXEC_ENABLE_EXTRA_TYPE_CHECKING ON CACHE BOOL "" FORCE)
add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/stdexec)

if (RADRAY_ENABLE_MIMALLOC)
    set(MI_BUILD_OBJECT OFF CACHE BOOL "" FORCE)
    set(MI_BUILD_SHARED OFF CACHE BOOL "" FORCE)
    set(MI_BUILD_TESTS OFF CACHE BOOL "" FORCE)
    set(MI_XMALLOC ON CACHE BOOL "" FORCE)
    set(MI_OPT_ARCH ON CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/mimalloc)
    radray_optimize_flags_library(mimalloc-static)
    radray_set_build_path(mimalloc-static)
endif()

if (RADRAY_ENABLE_DXC)
    if (NOT DEFINED RADRAY_SDK_dxc OR NOT DEFINED RADRAY_SDK_dxc_VERSION)
        message(FATAL_ERROR "Failed to load dxc info from sdk_project_manifest.json")
    endif()
    set(_DXC_ROOT "${CMAKE_SOURCE_DIR}/SDKs/dxc/v${RADRAY_SDK_dxc_VERSION}/extracted")
    set(_DXC_INCLUDE_DIR "${_DXC_ROOT}/include")
    set(_DXC_BIN_DIR "${_DXC_ROOT}/bin")
    set(_DXC_DLL "${_DXC_BIN_DIR}/dxcompiler.dll")
    set(_DXC_DXIL_DLL "${_DXC_BIN_DIR}/dxil.dll")
    add_library(dxc::dxcompiler INTERFACE IMPORTED)
    set_target_properties(dxc::dxcompiler PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${_DXC_INCLUDE_DIR}")
    set(RADRAY_DXC_RUNTIME_DLLS "${_DXC_DLL}")
    if (EXISTS "${_DXC_DXIL_DLL}")
        list(APPEND RADRAY_DXC_RUNTIME_DLLS "${_DXC_DXIL_DLL}")
    endif()
    set(RADRAY_DXC_RUNTIME_DLLS "${RADRAY_DXC_RUNTIME_DLLS}" CACHE INTERNAL "DXC runtime binaries")
endif()

if (RADRAY_ENABLE_ZLIB)
    set(ZLIB_BUILD_TESTING OFF CACHE BOOL "" FORCE)
    set(ZLIB_BUILD_SHARED OFF CACHE BOOL "" FORCE)
    set(ZLIB_INSTALL OFF CACHE BOOL "" FORCE)
    set(ZLIB_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/zlib)
    radray_optimize_flags_library(zlibstatic)
    radray_set_build_path(zlibstatic)
    if (TARGET zlibstatic AND NOT TARGET ZLIB::ZLIB)
        add_library(ZLIB::ZLIB ALIAS zlibstatic)
    endif()
    set(ZLIB_FOUND TRUE CACHE BOOL "ZLIB found" FORCE)
    set(ZLIB_INCLUDE_DIR "${RADRAY_THIRDPARTY_ROOT}/zlib" CACHE PATH "ZLIB include dir" FORCE)
    set(ZLIB_LIBRARY zlibstatic CACHE FILEPATH "ZLIB library" FORCE)
    set(ZLIB_LIBRARIES zlibstatic CACHE FILEPATH "ZLIB libraries" FORCE)
endif()

if (RADRAY_ENABLE_LIBPNG)
    set(PNG_TESTS OFF CACHE BOOL "" FORCE)
    set(PNG_TOOLS OFF CACHE BOOL "" FORCE)
    set(PNG_SHARED OFF CACHE BOOL "" FORCE)
    set(ZLIB_ROOT "${RADRAY_THIRDPARTY_ROOT}/zlib" CACHE PATH "ZLIB root" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/libpng)
    radray_optimize_flags_library(png_static)
    radray_set_build_path(png_static)
    if (TARGET png_static AND NOT TARGET PNG::PNG)
        add_library(PNG::PNG ALIAS png_static)
    endif()
    set(PNG_FOUND TRUE CACHE BOOL "PNG found" FORCE)
    set(PNG_INCLUDE_DIR "${RADRAY_THIRDPARTY_ROOT}/libpng" "${CMAKE_BINARY_DIR}/third_party/libpng" CACHE PATH "PNG include dir" FORCE)
    set(PNG_INCLUDE_DIRS "${PNG_INCLUDE_DIR}" CACHE PATH "PNG include dirs" FORCE)
    set(PNG_LIBRARY png_static CACHE FILEPATH "PNG library" FORCE)
    set(PNG_LIBRARIES png_static CACHE FILEPATH "PNG libraries" FORCE)
    set(PNG_PNG_INCLUDE_DIR "${PNG_INCLUDE_DIR}" CACHE PATH "PNG include dir" FORCE)
endif()

if (RADRAY_ENABLE_FREETYPE)
    set(FT_DISABLE_BZIP2 ON CACHE BOOL "" FORCE)
    set(FT_DISABLE_HARFBUZZ ON CACHE BOOL "" FORCE)
    set(FT_DYNAMIC_HARFBUZZ OFF CACHE BOOL "" FORCE)
    set(FT_DISABLE_BROTLI ON CACHE BOOL "" FORCE)
    set(FT_REQUIRE_ZLIB ON CACHE BOOL "" FORCE)
    set(FT_REQUIRE_PNG ON CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/freetype)
    radray_optimize_flags_library(freetype)
    radray_set_build_path(freetype)
endif()

if (RADRAY_ENABLE_VULKAN)
    set(PROJECT_IS_TOP_LEVEL OFF CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/Vulkan-Headers)
    if (DEFINED vulkan-headers_SOURCE_DIR)
        set(VULKAN_HEADERS_INSTALL_DIR "${vulkan-headers_SOURCE_DIR}" CACHE PATH "" FORCE)
    endif()
    set(VOLK_PULL_IN_VULKAN OFF CACHE BOOL "" FORCE)
    set(VOLK_HEADERS_ONLY ON CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/volk)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/VulkanMemoryAllocator)
endif()

if (RADRAY_ENABLE_DXC OR RADRAY_ENABLE_D3D12)
    set(DXHEADERS_BUILD_TEST OFF CACHE BOOL "" FORCE)
    set(DXHEADERS_INSTALL OFF CACHE BOOL "" FORCE)
    set(DXHEADERS_BUILD_GOOGLE_TEST OFF CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/DirectX-Headers)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/D3D12MemoryAllocator)
    target_compile_options(D3D12MemoryAllocator PRIVATE 
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<BOOL:${MSVC}>>:-Wno-unused-variable>
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<BOOL:${MSVC}>>:-Wno-switch>
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<BOOL:${MSVC}>>:-Wno-unused-const-variable>
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<BOOL:${MSVC}>>:-Wno-unused-function>)
    radray_optimize_flags_library(D3D12MemoryAllocator)
    radray_set_build_path(D3D12MemoryAllocator)
endif()

if (RADRAY_ENABLE_SPVCROSS)
    set(SPIRV_CROSS_CLI OFF CACHE BOOL "" FORCE)
    set(SPIRV_CROSS_ENABLE_TESTS OFF CACHE BOOL "" FORCE)
    set(SPIRV_CROSS_ENABLE_HLSL OFF CACHE BOOL "" FORCE)
    set(SPIRV_CROSS_ENABLE_CPP OFF CACHE BOOL "" FORCE)
    set(SPIRV_CROSS_ENABLE_REFLECT OFF CACHE BOOL "" FORCE)
    set(SPIRV_CROSS_ENABLE_C_API OFF CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/SPIRV-Cross)
    radray_optimize_flags_library(spirv-cross-core)
    radray_set_build_path(spirv-cross-core)
    radray_optimize_flags_library(spirv-cross-glsl)
    radray_set_build_path(spirv-cross-glsl)
    radray_optimize_flags_library(spirv-cross-msl)
    radray_set_build_path(spirv-cross-msl)
    radray_optimize_flags_library(spirv-cross-util)
    radray_set_build_path(spirv-cross-util)
endif()

if (RADRAY_BUILD_TESTS)
    set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
    set(BUILD_GMOCK OFF CACHE BOOL "" FORCE)
    set(INSTALL_GTEST OFF CACHE BOOL "" FORCE)
    add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/googletest)
    radray_optimize_flags_library(gtest)
    radray_set_build_path(gtest)
    radray_optimize_flags_library(gtest_main)
    radray_set_build_path(gtest_main)
    if (RADRAY_BUILD_BENCHMARKS)
        set(BENCHMARK_ENABLE_TESTING OFF CACHE BOOL "" FORCE)
        set(BENCHMARK_ENABLE_INSTALL OFF CACHE BOOL "" FORCE)
        set(BENCHMARK_INSTALL_DOCS OFF CACHE BOOL "" FORCE)
        set(BENCHMARK_INSTALL_TOOLS OFF CACHE BOOL "" FORCE)
        set(BENCHMARK_ENABLE_GTEST_TESTS OFF CACHE BOOL "" FORCE)
        set(BENCHMARK_USE_BUNDLED_GTEST OFF CACHE BOOL "" FORCE)
        add_subdirectory(${RADRAY_THIRDPARTY_ROOT}/googlebenchmark)
        radray_optimize_flags_library(benchmark)
        radray_set_build_path(benchmark)
    endif()
endif()

add_subdirectory(modules)
if (RADRAY_BUILD_TESTS)
    enable_testing()
    include(GoogleTest)
    add_subdirectory(tests)
    add_subdirectory(examples)
    if (RADRAY_BUILD_BENCHMARKS)
        add_subdirectory(benchmarks)
    endif()
endif()
