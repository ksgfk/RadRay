function(radray_example_files TARGET_NAME)
	if (ARGC LESS 2)
		message(FATAL_ERROR "radray_example_files: 需要 <TARGET_NAME> 和至少一个 <SRC>")
	endif()
	if (NOT TARGET ${TARGET_NAME})
		message(FATAL_ERROR "radray_example_files: 目标 ${TARGET_NAME} 未定义")
	endif()
	set(_target_name "${ARGV0}")
	list(REMOVE_AT ARGV 0)
	set(_sources ${ARGV})

	foreach(_src IN LISTS _sources)
		if (NOT IS_ABSOLUTE "${_src}")
			set(_abs_src "${CMAKE_CURRENT_SOURCE_DIR}/${_src}")
		else()
			set(_abs_src "${_src}")
		endif()
		if (NOT EXISTS "${_abs_src}")
			message(FATAL_ERROR "radray_example_files: 源不存在: ${_abs_src}")
		endif()
		if (IS_DIRECTORY "${_abs_src}")
			file(RELATIVE_PATH _rel_dir "${CMAKE_CURRENT_SOURCE_DIR}" "${_abs_src}")
			set(_dst_dir "${CMAKE_SOURCE_DIR}/assets/${_target_name}/${_rel_dir}")
			file(INSTALL DESTINATION "${_dst_dir}" TYPE DIRECTORY FILES "${_abs_src}/")
		else()
			file(RELATIVE_PATH _rel_file "${CMAKE_CURRENT_SOURCE_DIR}" "${_abs_src}")
			get_filename_component(_rel_dir "${_rel_file}" DIRECTORY)
			set(_dst_dir "${CMAKE_SOURCE_DIR}/assets/${_target_name}/${_rel_dir}")
			file(INSTALL DESTINATION "${_dst_dir}" TYPE FILE FILES "${_abs_src}")
		endif()
	endforeach()
endfunction()
